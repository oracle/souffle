// simple call-insensitive field-sensitive points-to analysis in Datalog
.type Variable 
.type Allocation
.type Field

.decl AssignAlloc(var:Variable, heap:Allocation) output
.decl Assign(source:Variable, destination:Variable) output
.decl PrimitiveAssign(source:Variable, dest:Variable) output
.decl Load(base:Variable, dest:Variable, field:Field) output
.decl Store(source:Variable, base:Variable, field:Field) output
.decl VarPointsTo(var:Variable, heap:Allocation) output
.decl Alias(x:Variable,y:Variable) output

Assign(var1, var2) :- 
  PrimitiveAssign(var1,var2). 

Alias(instanceVar, iVar) :- 
  VarPointsTo(instanceVar, instanceHeap), 
  VarPointsTo(iVar, instanceHeap).

VarPointsTo(var, heap) :-
  AssignAlloc(var, heap). 

VarPointsTo(var1, heap) :-
  Assign(var2, var1), 
  VarPointsTo(var2, heap).

Assign(var1, var2) :- 
  Store(var1, instanceVar2, field), 
  Alias(instanceVar2, instanceVar1), 
  Load(instanceVar1, var2, field).

#include "edb.dl"
