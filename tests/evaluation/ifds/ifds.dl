//
// Souffle - A Datalog Compiler
// Copyright (c) 2015, Oracle and/or its affiliates. All rights reserved
// Licensed under the Universal Permissive License v 1.0 as shown at:
// - https://opensource.org/licenses/UPL
// - <souffle root>/licenses/SOUFFLE-UPL.txt

 and/or its affiliates.
//
// All rights reserved.
//

// This program is a Datalog implementation of IFDS

.type String
.type Integer

.type Procedure = String
.type Node = String
.type DataFlowFact = String

.decl Node(n:Node)
.output Node()
.input Node()
.decl Procedure(p:Procedure)
.output Procedure()
.input Procedure()
.decl DataFlowFact(f:DataFlowFact)
.output DataFlowFact()
.input DataFlowFact()

//EDB relations

//Node n is the start node of procedure p
.decl StartNode(n:Node, p:Procedure)
.output StartNode()
.input StartNode()

//Node n is the return node of procedure p
.decl ReturnNode(n:Node, p:Procedure)
.output ReturnNode()
.input ReturnNode()

//Node n is a call node in procedure p
.decl CallNode(n:Node, p:Procedure)
.output CallNode()
.input CallNode()

//Map a call node m to its after-return node n
.decl ReturnSite(m:Node, n:Node)
.output ReturnSite()
.input ReturnSite()

//Map a node to its enclosing procedure
.decl ProcOf(n:Node, p:Procedure)
.output ProcOf()
.input ProcOf()

//Map a call node to its callee
.decl CalledProc(n:Node, p:Procedure)
.output CalledProc()
.input CalledProc()

//Data-flow functions in the form of ((m, d1) -> (n,d2))
.decl FlowEdge(m:Node, d1:DataFlowFact, n:Node, d2:DataFlowFact)
.output FlowEdge()
.input FlowEdge()

//IDB relations

//Summary data-flow edges in the form of ((m, d1) -> (n, d2))
.decl SummaryEdge(m:Node, d1:DataFlowFact, n:Node, d2:DataFlowFact)
.output SummaryEdge()

//PathEdge between two nodes
.decl PathEdge(m:Node, d1:DataFlowFact, n:Node, d2:DataFlowFact)
.output PathEdge()

//Initialisation
PathEdge("s_main", "0", "s_main", "0").

//Case n in Call_p
PathEdge(sq, d3, sq, d3) :- ProcOf(n, p),
                            CallNode(n, p),
						    StartNode(s, p),
						    PathEdge(s, d1, n, d2),
                            CalledProc(n, q),
                            StartNode(sq, q),
                            FlowEdge(n, d2, sq, d3).

PathEdge(s, d1, r, d3) :- ProcOf(n, p),
                          CallNode(n, p),
                          FlowEdge(n, d2, r, d3),
                          ReturnSite(n, r),
                          StartNode(s, p),
                          PathEdge(s, d1, n, d2).

PathEdge(s, d1, r, d3) :- ProcOf(n, p),
                          CallNode(n, p),
                          SummaryEdge(n, d2, r, d3),
                          ReturnSite(n, r),
                          StartNode(s, p),
                          PathEdge(s, d1, n, d2).

//Case n equals e_p
SummaryEdge(c, d4, r, d5) :- ReturnNode(n, p),
                             CalledProc(c, p),
                             StartNode(s, p),
                             PathEdge(s, d1, n, d2),
                             FlowEdge(c, d4, s, d1),
                             ReturnSite(c, r),
                             FlowEdge(n, d2, r, d5).

PathEdge(sq, d3, r, d5) :- ReturnNode(n, p),
                           CalledProc(c, p),
                           StartNode(s, p),
                           PathEdge(s, d1, n, d2),
                           FlowEdge(c, d4, s, d1),
                           ReturnSite(c, r),
                           FlowEdge(n, d2, r, d5),
                           SummaryEdge(c, d4, r, d5),
                           ProcOf(c, q),
                           StartNode(sq, q),
                           PathEdge(sq, d3, c, d4).

//Case n in (N_p - Call_p - e_p)
PathEdge(s, d1, m, d3) :- FlowEdge(n, d2, m, d3),
                          ProcOf(n, p),
                          !ReturnNode(n, p),
                          !CallNode(n, p),
                          StartNode(s, p),
                          PathEdge(s, d1, n, d2).

